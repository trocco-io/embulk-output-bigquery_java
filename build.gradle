plugins {
    id "com.github.johnrengelman.shadow" version "5.2.0" apply false
    id "java"
    id "maven-publish"
    id "com.diffplug.spotless" version "5.17.1"
    id "org.embulk.embulk-plugins" version "0.4.1"
    id "com.palantir.git-version" version "0.12.3"
    id "com.adarshr.test-logger" version "3.0.0"
}

repositories {
    mavenCentral()
}

group = "io.trocco"
description = "Bigquery output for embulk"

version = {
    def vd = versionDetails()
    if (vd.commitDistance == 0 && vd.lastTag ==~ /^v[0-9]+\.[0-9]+\.[0-9]+(\.[a-zA-Z0-9]+)?/) {
        vd.lastTag.substring(1)
    } else {
        "0.0.0.${vd.gitHash}.pre"
    }
}()

sourceCompatibility = 1.8
targetCompatibility = 1.8

tasks.withType(JavaCompile).configureEach {
    options.compilerArgs << "-Xlint:deprecation" << "-Xlint:unchecked"
}

dependencies {
    def embulkVersion = "0.10.42"
    compileOnly("org.embulk:embulk-api:${embulkVersion}")
    compileOnly("org.embulk:embulk-spi:${embulkVersion}")

    compile "org.embulk:embulk-util-config:0.3.4"
    compile "org.embulk:embulk-util-file:0.2.0"
    compile "org.embulk:embulk-util-text:0.2.0"
    compile "org.embulk:embulk-util-timestamp:0.3.0"
    compile "org.embulk:embulk-util-retryhelper:0.8.0"

    compile project(path: ":shadow-google-cloud-bigquery-helper", configuration: "shadow")

    // compile "YOUR_JAR_DEPENDENCY_GROUP:YOUR_JAR_DEPENDENCY_MODULE:YOUR_JAR_DEPENDENCY_VERSION"
    testImplementation "junit:junit:4.13.2"
    testImplementation "org.slf4j:slf4j-simple:1.7.30"
    testImplementation "org.mockito:mockito-core:1.10.19"
    testImplementation "org.embulk:embulk-core:${embulkVersion}"
    testImplementation "org.embulk:embulk-deps:${embulkVersion}"
    testImplementation "org.embulk:embulk-junit4:${embulkVersion}"
    testImplementation "org.embulk:embulk-input-file:0.11.1"
    testImplementation "org.embulk:embulk-parser-csv:0.11.6"
}

embulkPlugin {
    mainClass = "org.embulk.output.bigquery_java.BigqueryJavaOutputPlugin"
    category = "output"
    type = "bigquery_java"
}

publishing {
    publications {
        embulkPluginMaven(MavenPublication) {  // Publish it with "publishEmbulkPluginMavenPublicationToMavenRepository".
            from components.java  // Must be "components.java". The dependency modification works only for it.
        }
    }
    repositories {
        maven {
            url = "${project.buildDir}/mavenPublishLocal"
        }
    }
}

gem {
    from("LICENSE")  // Optional -- if you need other files in the gem.
    authors = [ "giwa" ]
    email = [ "ugw.gi.world@gmail.com" ]
    // "description" of the gem is copied from "description" of your Gradle project.
    summary = "Bigquery output plugin for Embulk"
    homepage = "https://github.com/trocco-io/embulk-output-bigquery_java"
    licenses = [ "Apache-2.0" ]
}

gemPush {
    host = "https://rubygems.org"
}

spotless {
    java {
        importOrder()
        removeUnusedImports()
        googleJavaFormat()
    }
}
