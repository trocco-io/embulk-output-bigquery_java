plugins {
    id "com.jfrog.bintray" version "1.1"
    id "com.github.jruby-gradle.base" version "1.5.0"
    id "com.github.johnrengelman.shadow" version "4.0.4"
    id "java"
    id "checkstyle"
}
import com.github.jrubygradle.JRubyExec
repositories {
    mavenCentral()
    jcenter()
}
configurations {
    provided
}

version = "0.0.3"

sourceCompatibility = 1.8
targetCompatibility = 1.8

// Relocate Guava packages since it's incompatible with Guava's version from Embulk
shadowJar {
    classifier = "shadow"
    exclude "org/embulk/plugin/**"

    dependencies {
        include dependency("com.google.guava:guava")
     //   include dependency("com.google.cloud:google-cloud-bigquery")
        include dependency("com.google.http-client:google-http-client")
    }

    relocate "com.google.common", "embulk.bigquery.com.google.common"
}


dependencies {
    compile "org.embulk:embulk-core:0.9.23"
    provided "org.embulk:embulk-core:0.9.23"

    compile 'com.fasterxml.jackson.core:jackson-core:2.10.2'
    compile 'com.google.cloud:google-cloud-bigquery:1.106.0'

    // compile "YOUR_JAR_DEPENDENCY_GROUP:YOUR_JAR_DEPENDENCY_MODULE:YOUR_JAR_DEPENDENCY_VERSION"
    testCompile "junit:junit:4.+"
}

task classpath(type: Copy, dependsOn: ["jar", "shadowJar"]) {
    doFirst { file("classpath").deleteDir() }
    from (configurations.runtime - configurations.provided
            + configurations.shadow
            - files(shadowJar.getIncludedDependencies())
            + files(shadowJar.archivePath))
    into "classpath"
}
clean { delete "classpath" }

checkstyle {
    configFile = file("${project.rootDir}/config/checkstyle/checkstyle.xml")
    toolVersion = '6.14.1'
}
checkstyleMain {
    configFile = file("${project.rootDir}/config/checkstyle/default.xml")
    ignoreFailures = true
}
checkstyleTest {
    configFile = file("${project.rootDir}/config/checkstyle/default.xml")
    ignoreFailures = true
}
task checkstyle(type: Checkstyle) {
    classpath = sourceSets.main.output + sourceSets.test.output
    source = sourceSets.main.allJava + sourceSets.test.allJava
}

task gem(type: JRubyExec, dependsOn: ["gemspec", "classpath"]) {
    jrubyArgs "-S"
    script "gem"
    scriptArgs "build", "${project.name}.gemspec"
    doLast { ant.move(file: "${project.name}-${project.version}.gem", todir: "pkg") }
}

task gemPush(type: JRubyExec, dependsOn: ["gem"]) {
    jrubyArgs "-S"
    script "gem"
    scriptArgs "push", "pkg/${project.name}-${project.version}.gem"
}

task "package"(dependsOn: ["gemspec", "classpath"]) {
    doLast {
        println "> Build succeeded."
        println "> You can run embulk with '-L ${file(".").absolutePath}' argument."
    }
}

task gemspec {
    ext.gemspecFile = file("${project.name}.gemspec")
    inputs.file "build.gradle"
    outputs.file gemspecFile
    doLast { gemspecFile.write($/
Gem::Specification.new do |spec|
  spec.name          = "${project.name}"
  spec.version       = "${project.version}"
  spec.authors       = ["giwa"]
  spec.summary       = %[Bigquery Java output plugin for Embulk]
  spec.description   = %[Dumps records to Bigquery Java.]
  spec.email         = ["ugw.gi.world@gmail.com"]
  spec.licenses      = ["MIT"]
  spec.homepage      = "https://github.com/trocco-io/embulk-output-bigquery_java"

  spec.files         = `git ls-files`.split("\n") + Dir["classpath/*.jar"]
  spec.test_files    = spec.files.grep(%r"^(test|spec)/")
  spec.require_paths = ["lib"]

  #spec.add_dependency 'YOUR_GEM_DEPENDENCY', ['~> YOUR_GEM_DEPENDENCY_VERSION']
  spec.add_development_dependency 'bundler', ['~> 1.0']
  spec.add_development_dependency 'rake', ['~> 12.0']
end
/$)
    }
}
clean { delete "${project.name}.gemspec" }
